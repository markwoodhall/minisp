(local commands (require :commands))
(local nvim (require "nvim"))
(local mimis (require :mimis))

(fn enable [])

(local aws-command "aws --no-cli-pager ")

(local get-profile (partial commands.get-command-value "--profile"))

;; logs
(fn log-groups [command]
  (let [profile (get-profile command)]
    (if profile
      (let [lgs (vim.fn.system (.. aws-command "--profile " profile " logs describe-log-groups | jq '.logGroups[].logGroupName'"))]
        (mimis.split lgs "\n"))
      [])))

;; sqs
(fn sqs-queues [command]
  (let [profile (get-profile command)]
    (if profile
      (let [lgs (vim.fn.system (.. aws-command "--profile " profile " sqs list-queues | jq '.QueueUrls[]'"))]
        (mimis.split lgs "\n"))
      [])))

;; ecs
(fn ecs-clusters [command]
  (let [profile (get-profile command)]
    (if profile
      (let [lgs (vim.fn.system (.. aws-command "--profile " profile " ecs list-clusters | jq '.clusterArns[]'"))]
        (mimis.split lgs "\n"))
      [])))

(fn ecs-services [command]
  (let [profile (get-profile command)
        cluster (commands.get-command-value "--cluster" command)]
    (if profile
      (let [lgs (vim.fn.system (.. aws-command "--profile " profile " ecs list-services --cluster " cluster " | jq '.serviceArns[]'"))]
        (mimis.split lgs "\n"))
      [])))

(fn ecs-tasks [command]
  (let [profile (get-profile command)
        cluster (commands.get-command-value "--cluster" command)]
    (if (and profile cluster)
      (let [lgs (vim.fn.system (.. aws-command "--profile " profile " ecs list-tasks --cluster " cluster " | jq '.taskArns[]'"))]
        (mimis.split lgs "\n"))
      [])))

;; rds
(fn db-instances [command]
  (let [profile (get-profile command) ]
    (if profile
      (let [lgs (vim.fn.system (.. aws-command "--profile " profile " rds describe-db-instances | jq '.DBInstances[].DBInstanceIdentifier'"))]
        (mimis.split lgs "\n"))
      [])))

(fn profiles []
  (let [lgs (vim.fn.system "cat ~/.aws/config | grep '\\[profile ' | sed -e 's/\\[//g' -e 's/\\]//g' -e 's/profile //g'")]
    (mimis.split lgs "\n")))

(fn completer [command]
  (let [command (string.match (vim.fn.substitute command "Aws" "aws" "") "aws.*")
        lgs (vim.fn.system (.. "COMMAND_LINE='" command "' aws_completer"))
        col (mimis.split lgs "\n")]
    (accumulate 
      [c []
       _ v (ipairs col)]
      [(string.gsub v "%s+" "") (unpack c)])))

(fn for-service [c service f]
  (match (commands.get-primary-command c)
    service (f c)
    _ []))

(fn for-command [c service command f]
  (match (commands.get-primary-command c)
    service (match (commands.get-sub-command c)
              command (f c)
              _ [])
    _ []))

(fn double-switch? [s]
  (and s (>= (length s) 2) (= (string.sub s 1 2) "--")))

(fn completion [_ c]
  (vim.fn.sort
    (let [c-parts (mimis.split c " ")
          last-part (mimis.last c-parts)
          enclosing-flag (when (not (double-switch? last-part))
                           (let [prev (mimis.nth c-parts (- (length c-parts) 1))]
                             (when (double-switch? prev) prev)))
          switch (or enclosing-flag last-part)
          with-defaults (fn [c]
                          [(unpack c)])]
      (case switch
        "--log-group-name" (log-groups c) 
        "--queue-url" (for-service c :sqs sqs-queues)
        "--cluster" (for-service c :ecs ecs-clusters)
        "--clusters" (for-service c :ecs ecs-clusters)
        "--service-name" (for-service c :ecs ecs-services)
        "--tasks" (for-service c :ecs ecs-tasks)
        "--db-instance-identifier" (with-defaults (db-instances c))
        "--attribute-names" (for-command c :sqs :get-queue-attributes (fn [_] ["All"])) 
        "--profile" (profiles)
        "--start-time" ["`date -d \"5 minutes ago\" +\\%s000`"
                        "`date -d \"15 minutes ago\" +\\%s000`"
                        "`date -d \"30 minutes ago\" +\\%s000`"
                        "`date -d \"45 minutes ago\" +\\%s000`"
                        "`date -d \"1 hour ago\" +\\%s000`"
                        "`date -d \"2 hour ago\" +\\%s000`"
                        "`date -d \"24 hours ago\" +\\%s000`"] 

        "--end-time" ["`date -d \"5 minutes ago\" +\\%s000`"
                      "`date -d \"15 minutes ago\" +\\%s000`"
                      "`date -d \"30 minutes ago\" +\\%s000`"
                      "`date -d \"45 minutes ago\" +\\%s000`"
                      "`date -d \"1 hour ago\" +\\%s000`"
                      "`date -d \"2 hour ago\" +\\%s000`"
                      "`date -d \"24 hours ago\" +\\%s000`"] 
        "|" (for-command c :logs :filter-log-events (fn [_] [" jq '.events[].message | fromjson | {timestamp, exception}'"
                                                             " jq '.events[].message | fromjson | {timestamp, message}'"]))
        _ (case (commands.get-last-double-switch c)
            "tasks" (with-defaults (ecs-tasks c))
            _ (with-defaults (completer (.. c ""))))))))

  (fn setup []
    (vim.api.nvim_create_user_command
      "Aws"
      (fn [opts]
        (let [args (accumulate 
                     [s ""
                      _ v (ipairs (?. opts :fargs))]
                     (.. s " " v))]
          (mimis.shell opts (.. aws-command args))
          (set nvim.bo.syntax :json)))
      {:bang false :desc "AWS command line wrapper" :nargs "*"
       :complete completion}))

  {: enable
   : profiles
   : setup }
